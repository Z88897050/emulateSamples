plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.jcraft:jsch:0.1.54'  //JSch is a pure Java implementation of SSH2
    /*Apache Commons Compress software defines an API
       for working with compression and archive formats. These include:
       bzip2, gzip, pack200, lzma, xz, Snappy, traditional Unix Compress, DEFLATE,
       DEFLATE64, LZ4, Brotli, Zstandard and ar, cpio, jar, tar, zip, dump, 7z, arj*/
    compile 'org.apache.commons:commons-compress:1.18'
    /*Apache Commons Net library contains a collection of network utilities
        and protocol implementations. Supported protocols include:
        Echo, Finger, FTP, NNTP, NTP, POP3(S), SMTP(S), Telnet, Whois*/
    compile 'commons-net:commons-net:3.6'
    /*Apache Commons CLI provides a simple API for presenting, processing and
         validating a command line interface.*/
//    Java命令行选项解析还有Args4J,JCommander两者都基于注解
    compile 'commons-cli:commons-cli:1.4'
}

group = 'functions'
version = ''
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

jar {
    baseName 'jsch'
    manifest {
        attributes 'Main-Class': 'impl.Main'
    }
}

test.enabled = false

task copyJar(type: Copy) {
    from configurations.runtime
    from('build/libs') {
        include('*.jar')
    }
    into 'build/lib'
}
task copyOtherFile(type: Copy) {
//    from(rootDir) {
//        include 'bin/**'
//    }
    from 'src/main/resources/jsch.sh'
    into 'build/bin'
}
copyJar.mustRunAfter(build)
task release(dependsOn: [build, copyJar, copyOtherFile]) {
    doLast {
        new File(buildDir.getAbsolutePath()).eachFile { f ->
            if (f.isDirectory()) {
                String name = f.getName()
                if (name == "classes" || name == "tmp" || name == "libs" || name == "resources") {
                    f.deleteDir()
                }
            }
        }
    }
}